// Copyright by Barry G. Becker, 2013. Licensed under MIT License: http://www.opensource.org/licenses/MIT

/**
 * This is common gradle build configuration for all bb4 projects.
 * Any project on github can include it using
 * apply from: 'https://raw.github.com/barrybecker4/bb4-common/master/bb4.gradle'
 */
apply plugin: 'java'
sourceCompatibility = '1.6'
targetCompatibility = '1.6'
apply plugin: 'project-report'
apply plugin: 'application'
apply plugin: 'idea'


// all bb4 projects use this directory layout.
sourceSets {
    main {
        java {
            srcDir 'source'
        }
        resources {
            srcDir 'source'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test'
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://repo1.maven.org/maven2/"
        url "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases"
    }
}

dependencies {
    compile 'commons-collections:commons-collections:3.2'
    compile 'java3d:vecmath:1.3.1'
    testCompile 'junit:junit:3.+'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Create a jar file containing all project sources'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Create a jar file containing all project javadoc'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// These artifacts need to be published so other projects can depend on them
artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

javadoc.doLast {
    description = 'Copy images from source to javadoc in case they are referenced by documenation'
    copy {
        from sourceSets.main.allSource
        into javadoc.destinationDir
        include '**/package.png'
    }
}

clean.doLast {
    // project. is needed to disambiguate from the clean tasks existing delete.
    project.delete(project.repositories.fileRepo.dirs)
}

task copyToLib(dependsOn: jar, type: Copy) {
    description = "Copies the downloaded dependencies to the lib directory so they can be deployed"
    into "build/libs"
    from configurations.runtime
}