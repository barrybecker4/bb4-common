// Copyright by Barry G. Becker, 2013. Licensed under MIT License: http://www.opensource.org/licenses/MIT

/**
 * This is common gradle build configuration for all bb4 projects.
 * Any project on github can include it using
 * apply from: 'https://raw.github.com/barrybecker4/bb4-common/master/bb4.gradle'
 */
apply plugin: 'java'
sourceCompatibility = '1.6'
targetCompatibility = '1.6'
apply plugin: 'project-report'
apply plugin: 'application'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'


// all bb4 projects use this directory layout.
sourceSets {
    main {
        java {
            srcDir 'source'
        }
        resources {
            srcDir 'source'
        }
    }
    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDir 'test'
        }
    }
}

repositories {
    mavenCentral()
    maven {
        // this is where bb4 release artifacts will come from
        mavenRepo urls: [
            "http://repo1.maven.org/maven2/",
            // this is where SNAPSHOT releases will come from
            "https://oss.sonatype.org/content/groups/staging",
            // some thirdparty jars from here
            "https://repository.jboss.org/nexus/content/repositories/thirdparty-releases"
        ]
    }
}

dependencies {
    compile 'commons-collections:commons-collections:3.2'
    compile 'java3d:vecmath:1.3.1'
    testCompile 'junit:junit:3.+'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Create a jar file containing all project javadoc. Needed for publishing to Sonatype.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Create a jar file containing all project sources. Needed for publishing to Sonatype.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

// These artifacts need to be published so other projects can depend on them
artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

javadoc.doLast {
    description = 'Copy images from source to javadoc in case they are referenced by documenation'
    copy {
        from sourceSets.main.allSource
        into javadoc.destinationDir
        include '**/package.png'
    }
}

task copyToLib(dependsOn: jar, type: Copy) {
    description = "Copies the downloaded dependencies to the lib directory so they can be deployed"
    into "build/libs"
    from configurations.runtime
}

/**
 * Used to publish artifacts to Sonatype repository
 * See http://jedicoder.blogspot.com/2011/11/automated-gradle-project-deployment-to.html
 * Go to https://oss.sonatype.org to view staged repositories after publishing.
 */
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                 name 'bb4-common'
                 packaging 'jar'
                 description 'Common bb4 java code. Library project.'
                 url "https://github.com/barrybecker4/${archivesBaseName}"

                 scm {
                     url "scm:git@github.com:barrybecker4/${archivesBaseName}.git"
                     connection "scm:git@github.com:barrybecker4/${archivesBaseName}.git"
                     developerConnection "scm:git@github.com:barrybecker4/${archivesBaseName}.git"
                 }

                 licenses {
                     license {
                         name 'The MIT license'
                         url 'http://www.opensource.org/licenses/MIT'
                         distribution 'repo'
                     }
                 }

                 developers {
                     developer {
                         id 'barrybecker4'
                         name 'Barry Becker'
                     }
                     developer {
                         id 'wkeese'
                         name 'Bill Keese'
                     }
                 }
            }
        }
    }
}

task publishArtifacts (dependsOn:uploadArchives) {
    description = 'Publish artifacts to Sonatype repository'
}