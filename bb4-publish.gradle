// Copyright by Barry G. Becker, 2013. Licensed under MIT License: http://www.opensource.org/licenses/MIT

/**
 * This is common gradle build file used to publish bb4 artifacts.
 * Any project on github can include it using
 * apply from: 'https://raw.github.com/barrybecker4/bb4-common/master/bb4-publish.gradle'
 */

/** javadoc jar used for the whole project. Used only if appMap was not specified. */
task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Create a jar file containing all project javadoc. Needed by Sonatype release.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

/** sources jar used for the whole project. UUsed only if appMap was not specified. */
task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Create a jar file containing all project sources. Needed by Sonatype release.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.doLast {
    description = 'Copy images from source to javadoc in case they are referenced by documentation'
    copy {
        from sourceSets.main.allSource
        into javadoc.destinationDir
        include '**/package.png'
    }
}

/**
 * If a project has multiple apps, it should define them
 * in an map called appMap before applying this bb4-publish.gradle file.
 */
if (hasProperty('appMap')) {
    // skip building the default jars that contains everything if there is a specific list
    jar.enabled = false
    sourcesJar.enabled = false
    javadocJar.enabled = false
} else {
    ext.appMap = new HashMap()
}

/** create jar, javadocJar, and sourceJar for every entry in appMap (if there are any). */
appMap.each { String key, Map value ->
    task "${key}Jar"(type: Jar)  {
        baseName = "bb4-${key}"
        from sourceSets.main.output
        include value['include']
        if (value.containsKey('exclude'))
            exclude value['exclude']
        manifest {
            attributes 'Implementation-Title': 'Puzzle code',
                'Implementation-Version': version,
                provider: 'gradle'
        }
    }
    task "${key}JavadocJar"(type: Jar, dependsOn: javadoc) {
        description = "Create a javadoc jar file for ${key}. Needed for publishing to Sonatype."
        classifier = 'javadoc'
        baseName = "bb4-${key}"
        from javadoc.destinationDir
        include value['include']
    }
    task "${key}SourcesJar"(type: Jar, dependsOn: classes) {
        description = "Create a source jar file for ${key}. Needed for publishing to Sonatype."
        classifier = 'sources'
        baseName = "bb4-${key}"
        from sourceSets.main.allSource
        include value['include']
        if (value.containsKey('exclude'))
            exclude value['exclude']
    }
}

/**
 * These artifacts need to be published so other projects can depend on them.
 * The default jar is only include if there is not an explicit appMap.
 */
artifacts {
    if (appMap.size() == 0) {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    appMap.each { String key, Map value ->
        archives tasks["${key}Jar"]
        archives tasks["${key}JavadocJar"]
        archives tasks["${key}SourcesJar"]
    }
}

/** bb4 pom configuration for all artifacts */
def pomConfig = {
    name "${archivesBaseName}"
    packaging 'jar'
    description 'Common bb4 java code. Library project.'
    url "https://github.com/barrybecker4/${archivesBaseName}"

    // workaround to get changing snapshots to be updated
    properties {
       'timestamp' String.valueOf(System.currentTimeMillis())
    }

    scm {
        url "scm:git@github.com:barrybecker4/${archivesBaseName}.git"
        connection "scm:git@github.com:barrybecker4/${archivesBaseName}.git"
        developerConnection "scm:git@github.com:barrybecker4/${archivesBaseName}.git"
    }

    licenses {
        license {
            name 'The MIT license'
            url 'http://www.opensource.org/licenses/MIT'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'barrybecker4'
            name 'Barry Becker'
        }
    }
}

configure(install.repositories.mavenInstaller) {
    pom.project pomConfig
}

/**
 * Used to publish artifacts to Sonatype repository
 * See http://jedicoder.blogspot.com/2011/11/automated-gradle-project-deployment-to.html
 * Go to https://oss.sonatype.org to view staged repositories after publishing.
 */
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project pomConfig

            // creates filters for each jar produced by appMap entries (if any)
            appMap.each { String key, Map value ->
                def jarName = "bb4-$key"
                def jarPom = addFilter("${key}Jar") {artifact, file ->
                    artifact.name == jarName
                }
                jarPom.artifactId = jarName
                jarPom.project pomConfig
                jarPom.name = "bb4-$key"

            }
        }
    }
}

/** release jars need to be signed when published to sonatype so the publisher can be verified. */
signing {
    required { isReleaseVersion }
    if (appMap.size() == 0)  {
        sign jar
        sign sourcesJar
        sign javadocJar
    }

    appMap.each { String key, Map value ->
        sign tasks["${key}Jar"]
        sign tasks["${key}JavadocJar"]
        sign tasks["${key}SourcesJar"]
    }
}

/** Effectively the same as uploadArchives, but I like this name better */
task publishArtifacts (dependsOn:uploadArchives) {
    description = 'Publish artifacts to Sonatype repository'
}