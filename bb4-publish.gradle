// Copyright by Barry G. Becker, 2013 - 2018. Licensed under MIT License: http://www.opensource.org/licenses/MIT

/**
 * This common gradle build file is used to publish bb4 artifacts to Sonatype, and also deploy them to a website.
 * Any project on github can include it using
 * apply from: 'https://raw.githubusercontent.com/bb4/bb4-common/master/bb4-publish.gradle'
 */

/** javadoc jar used for the whole project. Used only if appMap was not specified. */
task javadocJar(type: Jar, dependsOn: javadoc) {
    description = 'Create a jar file containing all project javadoc. Needed by Sonatype release.'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

/** scaladoc jar used for the whole project. Used only if appMap was not specified. */
task scaladocJar(type: Jar, dependsOn: scaladoc) {
    description = 'Create a jar file containing all project scaladoc. Needed by Sonatype release.'
    classifier = 'scaladoc'
    from scaladoc.destinationDir
}

/** sources jar used for the whole project. Used only if appMap was not specified. */
task sourcesJar(type: Jar, dependsOn: classes) {
    description = 'Create a jar file containing all project sources. Needed by Sonatype release.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.doLast {
    description = 'Copy images from source to javadoc in case they are referenced by documentation'
    copy {
        from sourceSets.main.allSource
        into javadoc.destinationDir
        include '**/package.png'
    }
}

/**
 * If a project has multiple apps, it should define them in a map called appMap before
 * applying this bb4-publish.gradle file.
 * Skip building the default jars that contain everything if there is a specific list.
 */
if (hasProperty('appMap')) {
    jar.enabled = false
    sourcesJar.enabled = false
    javadocJar.enabled = false
    scaladocJar.enabled = false
} else {
    ext.appMap = new HashMap()
}

/**
 * Create jar, java/scaladocJar, and sourceJar for every entry in appMap (if there are any).
 * Consider setting the Permissions attribute to "all-permissions" if file access is needed.
 */
appMap.each { String key, Map value ->
    task "${key}Jar"(type: Jar)  {
        baseName = "bb4-${key}"
        from sourceSets.main.output
        value['include'].each { pattern -> include pattern }
        if (value.containsKey('exclude'))
            value['exclude'].each { pattern -> exclude pattern }
        manifest {
            attributes 'Implementation-Title': "${key} code",
                    //'Permissions': "sandbox",
                    'Implementation-Version': version,
                    'Application-Name': "${key}",
                    'Built-By': System.properties['user.name'],
                    'provider': 'gradle'
        }
    }
    task "${key}JavadocJar"(type: Jar, dependsOn: javadoc) {
        description = "Creates a javadoc jar file for ${key}. Needed for publishing to Sonatype."
        classifier = 'javadoc'
        baseName = "bb4-${key}"
        from javadoc.destinationDir
        value['include'].each { pattern -> include pattern }
    }
    task "${key}ScaladocJar"(type: Jar, dependsOn: scaladoc) {
        description = "Creates a scaladoc jar file for ${key}. Needed for publishing to Sonatype."
        classifier = 'scaladoc'
        baseName = "bb4-${key}"
        from javadoc.destinationDir
        value['include'].each { pattern -> include pattern }
    }
    task "${key}SourcesJar"(type: Jar, dependsOn: classes) {
        description = "Creates a source jar file for ${key}. Needed for publishing to Sonatype."
        classifier = 'sources'
        baseName = "bb4-${key}"
        from sourceSets.main.allSource
        value['include'].each { pattern -> include pattern }
        if (value.containsKey('exclude'))
            value['exclude'].each { pattern -> exclude pattern }
    }
}
ext.distributionDir = "dist"
ext.projectDistributionDir = "$distributionDir/${project.name}"
ext.schemaDistributionDir = "$distributionDir/schema"
import org.apache.tools.ant.filters.ReplaceTokens


// when copying the html, {version} is replaced with ${project.version}
task deployHtml(type: Copy, description: "Copy html related files to $projectDistributionDir") {
    from "source/html"
    into "$projectDistributionDir"
    include '*.*'
    filter(ReplaceTokens, tokens: [version: project.version])
}

// Copy XMl schema files to the dist directory so they can be referenced with an http namespace.
task deploySchemas(type:Copy, description: "Copy schema files to $schemaDistributionDir") {
    from fileTree("$project.projectDir").include("**/*.dtd", "**/*.xsd").files
    into "$schemaDistributionDir"
}

task deployZip(type: Copy, description: "Copy zip of jars and run scripts")  {
    from "build/distributions"
    into "$projectDistributionDir"
    include 'bb4-*.zip'
}

task deploy(dependsOn: [build, deployHtml, deploySchemas, deployZip],
        description: "Deploys all web-deployment files to $distributionDir so they can be bulk copied to a website") {
}

// Make sure everything in lib is in CLASSPATH (see https://gist.github.com/jlmelville/2bfe9277e9e2c0ff79b6)
tasks.withType(CreateStartScripts).each { task ->
    task.doLast {
        task.windowsScript.write task.windowsScript.text.replaceFirst(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, { "${it[1]}*" })
        task.unixScript.write task.unixScript.text.replaceFirst(/(CLASSPATH=.APP_HOME\/lib\/).*/, { "CLASSPATH=\$(echo \$APP_HOME/lib/*.jar | tr ' ' ':')" })
    }
}

// make sure that all generated jar files (from appMap) also get into the distributionDir
applicationDistribution.from("build/libs") {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    exclude "*.asc"
    exclude "*-sources.jar"
    exclude "*-scaladoc.jar"
    exclude "*-javadoc.jar"
    into "lib"
}


/**
 * These artifacts need to be published so other projects can depend on them.
 * The default jar is only included if there is not an explicit appMap.
 */
artifacts {
    if (appMap.size() == 0) {
        archives jar
        archives sourcesJar
        archives javadocJar
        archives scaladocJar
    }

    appMap.each { String key, Map value ->
        archives tasks["${key}Jar"]
        archives tasks["${key}JavadocJar"]
        archives tasks["${key}ScaladocJar"]
        archives tasks["${key}SourcesJar"]
        archives file("build/libs/bb4-${key}-${version}.jar.asc")
    }
}

/** release jars need to be signed when published to sonatype so the publisher can be verified. */
signing {
    required {
        isReleaseVersion
    }
    sign configurations.archives
}

/** bb4 pom configuration for core jar artifacts */
def jarPomConfig = {
    name "${project.name}"
    groupId 'com.barrybecker4'
    packaging 'jar'
    description "${project.name} java code."
    url "https://github.com/barrybecker4/${project.name}"

    // workaround to get changing snapshots to be updated.
    // Maybe not needed if --refresh-dependencies is used.
    properties {
       'timestamp' String.valueOf(System.currentTimeMillis())
    }

    scm {
        url "scm:git@github.com:barrybecker4/${project.name}.git"
        connection "scm:git@github.com:barrybecker4/${project.name}.git"
        developerConnection "scm:git@github.com:barrybecker4/${project.name}.git"
    }

    licenses {
        license {
            name 'The MIT license'
            url 'http://www.opensource.org/licenses/MIT'
            distribution 'repo'
        }
    }

    developers {
        developer {
            id 'barrybecker4'
            name 'Barry Becker'
            email 'barrybecker4@gmail.com'
        }
    }
}

configure(install.repositories.mavenInstaller) {
    pom.project jarPomConfig
}

/**
 * Used to publish artifacts to Sonatype repository
 * See http://jedicoder.blogspot.com/2011/11/automated-gradle-project-deployment-to.html
 * Go to https://oss.sonatype.org to view staged repositories after publishing.
 */
final def archives = uploadArchives {
    repositories {
        mavenDeployer {

            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project jarPomConfig

            // creates filters for each jar produced by appMap entries (if any)
            appMap.each { String key, Map value ->
                def jarName = "bb4-$key"
                def jarPom = addFilter("${key}Jar") { artifact, file ->
                    artifact.name == jarName
                }
                jarPom.project jarPomConfig
                jarPom.artifactId = jarName
            }
        }
    }
}
archives

/** Effectively the same as uploadArchives, but I like this name better */
task publishArtifacts (dependsOn:uploadArchives) {
    description = 'Publish artifacts to Sonatype repository'
}
